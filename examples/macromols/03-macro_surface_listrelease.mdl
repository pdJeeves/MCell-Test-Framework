/****************************************************************************
 * Test 03: Surface macromolecule list release.
 *
 *    This test is to ensure that surface macromolecule list releases work
 *    properly.  The information is validated based on the time 0 viz and
 *    reaction data output.  The complexes are at the center of each face, the
 *    middle of each edge, and on each corner of the box.  The subunits should
 *    conform to the corners of the box as appropriate.  Molecule positions
 *    will be validated, as will the counts of molecules actually released in
 *    each orientation, and the fraction of dimer pairs exhibiting each
 *    possible orientation combination.
 *
 * Author: Jed Wing <jed@salk.edu>
 * Date:   2008-04-04
 ****************************************************************************/

basename = "03-macro_surface_listrelease"
countdir = "dat/" & basename & "/"
vizdir   = "./viz_dat/" & basename & "/"
sprintf(seed, "%04g", SEED)
dt = 1e-6
iterations=1

TIME_STEP = dt
ITERATIONS = iterations
GRID_DENSITY = 15000
ACCURATE_3D_REACTIONS = FALSE

PARTITION_X = [[-0.5001 TO 0.50001 STEP 0.02]]
PARTITION_Y = [[-0.5001 TO 0.50001 STEP 0.02]]
PARTITION_Z = [[-0.5001 TO 0.50001 STEP 0.02]]

DEFINE_MOLECULES {
  camkii_subunit  { DIFFUSION_CONSTANT_2D = 0 }
}

DEFINE_COMPLEX_MOLECULE camkii {
  NUMBER_OF_SUBUNITS = [2, 6]
  SUBUNIT[1  , 1  ] = camkii_subunit'
  SUBUNIT[2  , 1  ] = camkii_subunit,
  SUBUNIT[1:2, 2  ] = camkii_subunit,
  SUBUNIT[1:2, 3:6] = camkii_subunit'
  SHAPE {
    SUBUNIT[1, 1] = [-.1100,  .0000, 0]
    SUBUNIT[1, 2] = [-.0678,  .0866, 0]
    SUBUNIT[1, 3] = [ .0678,  .0866, 0]
    SUBUNIT[1, 4] = [ .1100,  .0000, 0]
    SUBUNIT[1, 5] = [ .0678, -.0866, 0]
    SUBUNIT[1, 6] = [-.0678, -.0866, 0]

    SUBUNIT[2, 1] = [-.1100,  .0000, 0]
    SUBUNIT[2, 2] = [-.0678,  .0866, 0]
    SUBUNIT[2, 3] = [ .0678,  .0866, 0]
    SUBUNIT[2, 4] = [ .1100,  .0000, 0]
    SUBUNIT[2, 5] = [ .0678, -.0866, 0]
    SUBUNIT[2, 6] = [-.0678, -.0866, 0]
  }
  SUBUNIT_RELATIONSHIPS {
    dimer_partner = [+1,  0]
  }
  RATE_RULES {
  }
}

mainbox OBJECT {
  b BOX {
    CORNERS = [ -0.5, -0.5, -0.5 ], [ 0.5, 0.5, 0.5 ]
  }
}

INSTANTIATE world OBJECT
{
  mainbox OBJECT mainbox {
  }

  rs1 RELEASE_SITE {
    SHAPE = LIST
    MOLECULE_POSITIONS {
        camkii'   [-0.5, -0.5, -0.5]
        camkii'   [-0.5, -0.5,  0.5]
        camkii'   [-0.5,  0.5, -0.5]
        camkii'   [-0.5,  0.5,  0.5]
        camkii'   [ 0.5, -0.5, -0.5]
        camkii'   [ 0.5, -0.5,  0.5]
        camkii'   [ 0.5,  0.5, -0.5]
        camkii'   [ 0.5,  0.5,  0.5]
        camkii'   [-0.5, -0.5,  0.0]
        camkii'   [-0.5,  0.5,  0.0]
        camkii'   [ 0.5, -0.5,  0.0]
        camkii'   [ 0.5,  0.5,  0.0]
        camkii'   [-0.5,  0.0, -0.5]
        camkii'   [-0.5,  0.0,  0.5]
        camkii'   [ 0.5,  0.0, -0.5]
        camkii'   [ 0.5,  0.0,  0.5]
        camkii'   [ 0.0, -0.5, -0.5]
        camkii'   [ 0.0, -0.5,  0.5]
        camkii'   [ 0.0,  0.5, -0.5]
        camkii'   [ 0.0,  0.5,  0.5]
        camkii'   [ 0.0, -0.5,  0.0]
        camkii'   [ 0.0,  0.5,  0.0]
        camkii'   [ 0.0,  0.0, -0.5]
        camkii'   [ 0.0,  0.0,  0.5]
        camkii'   [-0.5,  0.0,  0.0]
        camkii'   [-0.5,  0.0,  0.0]
    }
  }
}

VIZ_OUTPUT
{
  MODE = ASCII
  FILENAME = "molecules"
  MOLECULES {
    NAME_LIST { camkii=1 camkii_subunit=2 }
    ITERATION_NUMBERS { ALL_DATA @ [0] }
  }
}

REACTION_DATA_OUTPUT
{
    OUTPUT_BUFFER_SIZE = 1000
    ITERATION_LIST=[0]
    HEADER = "# "
    {
      COUNT[camkii, WORLD]                                                               : "complexes",
      COUNT[camkii_subunit, WORLD]                                                       : "subunits",
      COUNT[SUBUNIT{camkii : camkii_subunit' [dimer_partner == camkii_subunit']}, WORLD] : "00",
      COUNT[SUBUNIT{camkii : camkii_subunit' [dimer_partner == camkii_subunit,]}, WORLD] : "01",
      COUNT[SUBUNIT{camkii : camkii_subunit, [dimer_partner == camkii_subunit,]}, WORLD] : "11"
    } => "counts.txt"
}
