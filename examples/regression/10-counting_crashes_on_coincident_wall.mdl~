/****************************************************************************
 * Regression test 10: Counting crashes if a transparent wall is very very
 *    nearly coincident with a partition boundary.
 *
 *    Failure: Crash and burn, in that order.
 *    Success: Run completes successfully.
 *
 * Author: Jed Wing <jed@salk.edu>
 * Date:   2009-02-13
 ****************************************************************************/

ITERATIONS = 100
TIME_STEP = 1e-6
PARTITION_X = [-1, 0, 1]
PARTITION_Y = [-1, 0, 1]
PARTITION_Z = [-1, 0, 1]

DEFINE_MOLECULE a { DIFFUSION_CONSTANT_3D = 6e-6 }

DEFINE_SURFACE_CLASS trans { TRANSPARENT = a; }

INSTANTIATE world OBJECT {
  cracker POLYGON_LIST {
    VERTEX_LIST {
      // DO NOT CHANGE THE MAGIC NUMBER
      [3.0519440883836279e-6, -1, -1]
      [3.0519440883836280e-6, -1,  1]
      [3.0519440883836280e-6,  1, -1]
      [3.0519440883836281e-6,  1,  1]
    }

    ELEMENT_CONNECTIONS {
      [0, 3, 2]
      [3, 0, 1]
    }

    DEFINE_SURFACE_REGIONS {
      all {
        INCLUDE_ELEMENTS = [ ALL_ELEMENTS ]
        SURFACE_CLASS = trans
      }
    }

  }

  boxy BOX {
    CORNERS = [-0.002, -0.002, -0.002], [0.002, 0.002, 0.002]
  }

  rs RELEASE_SITE {
    SHAPE = SPHERICAL
    LOCATION = [0, 0, 0.001]
    SITE_RADIUS = 0
    NUMBER_TO_RELEASE = 20
    MOLECULE = a
  }
}

REACTION_DATA_OUTPUT {
  OUTPUT_BUFFER_SIZE = 100
  STEP = 1e-6
  {COUNT[a,world.cracker,ALL_HITS]} => "poly_w_cracker.txt"
}
